// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlin_version = '1.8.22'
        dokka_version = '1.9.10'
        retrofit_version = '2.11.0'
        okhttp_version = '4.12.0'
        compose_version = '1.4.3'
        moshi_version = '1.15.1'
        kotlin_coroutines_version = '1.7.2'
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://storage.googleapis.com/r8-releases/raw")
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version")
        classpath("org.jetbrains.dokka:versioning-plugin:$dokka_version")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        constraints {
            // Force upgrades of AGP dependencies
            classpath("org.apache.commons:commons-compress:1.26.2")
            classpath("org.bitbucket.b_c:jose4j:0.9.6")
            classpath("io.netty:netty-handler:4.1.111.Final")
            classpath("io.netty:netty-handler-proxy:4.1.111.Final")
            classpath("io.netty:netty-codec-http2:4.1.111.Final")
            classpath("org.bouncycastle:bcpkix-jdk18on:1.78.1")
            classpath("com.google.jimfs:jimfs:1.3.0")
            classpath("io.grpc:grpc-netty:1.65.0")
            classpath("com.google.guava:guava:32.0.1-jre")
        }
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
    id "org.jlleitschuh.gradle.ktlint" version "12.1.0"
    id "io.gitlab.arturbosch.detekt" version "1.23.6"
    //id 'nl.neotech.plugin.rootcoverage' version '1.6.0'
    id "org.jetbrains.dokka" version "$dokka_version"
    id 'com.google.dagger.hilt.android' version '2.44' apply false
}
/*
rootCoverage {
    buildVariant "debug"
    excludes.addAll(
            "models/*",
    )
    generateHtml true
    generateXml true
    executeAndroidTests false
    executeUnitTests true
    includeUnitTestResults true
    includeNoLocationClasses true
}
*/
apply from: "${rootDir}/scripts/publish-root.gradle"

subprojects {
    tasks.withType(Javadoc).all { enabled = false }
    apply plugin: "org.jlleitschuh.gradle.ktlint" // Version should be inherited from parent
    apply plugin: "io.gitlab.arturbosch.detekt"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    // Optionally configure plugin
    ktlint {
        android = true
        version = "1.2.1"
    }
    configurations.all {
        resolutionStrategy {
            force("com.fasterxml.woodstox:woodstox-core:6.4.0")
            force("io.grpc:grpc-netty:1.65.0")
            force("com.google.guava:guava:32.0.1-jre")
            force("io.netty:netty-codec-http:4.1.111.Final")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.register("runOnGitHub") {
    dependsOn(
            ":source:sdk:lint",
            ":source:sdk:ktlintCheck",
            ":source:sdk:testDebugUnitTest",
    )
    group = "custom"
    description = "\$ ./gradlew runOnGitHub # runs on GitHub Action"
}

tasks.dokkaHtmlMultiModule.configure {
    outputDirectory.set(file("$projectDir/docs"))
}

// Create variables with empty default values
ext["STYTCH_PUBLIC_TOKEN"] = ""
ext["GOOGLE_OAUTH_CLIENT_ID"] = ""
ext["STYTCH_B2B_PUBLIC_TOKEN"] = ""
ext["STYTCH_B2B_ORG_ID"] = ""
ext["UI_GOOGLE_CLIENT_ID"] = ""
ext["PASSKEYS_DOMAIN"] = ""

File localProperties = project.rootProject.file("local.properties")
if (localProperties.exists()) {
    // Read local.properties file first if it exists
    Properties p = new Properties()
    new FileInputStream(localProperties).withCloseable { is -> p.load(is) }
    p.each { name, value -> ext[name] = value }
} else {
    // Use envvars
    ext["STYTCH_PUBLIC_TOKEN"] = System.getenv('STYTCH_PUBLIC_TOKEN')
    ext["GOOGLE_OAUTH_CLIENT_ID"] = System.getenv('GOOGLE_OAUTH_CLIENT_ID')
    ext["STYTCH_B2B_PUBLIC_TOKEN"] = System.getenv('STYTCH_B2B_PUBLIC_TOKEN')
    ext["STYTCH_B2B_ORG_ID"] = System.getenv('STYTCH_B2B_ORG_ID')
    ext["UI_GOOGLE_CLIENT_ID"] = System.getenv('UI_GOOGLE_CLIENT_ID')
    ext["PASSKEYS_DOMAIN"] = System.getenv('PASSKEYS_DOMAIN')
}